# Maintainer: Daniel Carl
pkgname=vimb-git
_pkgname=vimb
pkgver=2.11.r14.g8e28af1
pkgrel=1
pkgdesc="Vimb is a WebKit-based web browser"
arch=('i686' 'x86_64')
url="http://fanglingsu.github.io/vimb"
license=('GPL3')
depends=('webkitgtk2')
provides=('vimb')
conflicts=('vimb')
#source=('git://github.com/fanglingsu/vimb.git#branch=fix-file-locking')
source=('git://github.com/fanglingsu/vimb.git')
md5sums=('SKIP')
#options=(debug !strip)

pkgver() {
    cd "${_pkgname}"
    git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
    if [ -e $startdir/config.h ]; then
        cp "$startdir/config.h" "$_pkgname/src"
    fi
}

build() {
	cd "$_pkgname"
	make
}

package() {
	cd "$_pkgname"
	make DESTDIR="$pkgdir" PREFIX="/usr" install
}

